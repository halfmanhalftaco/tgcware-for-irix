--- x11vnc-0.8.1/configure.ac.trio	2006-06-24 16:33:53.445200000 +0200
+++ x11vnc-0.8.1/configure.ac	2006-06-24 16:35:00.045200000 +0200
@@ -466,6 +466,9 @@
     ;;
 esac
 
+# snprintf/vsnprintf
+NEED_TRIO_CHECK
+
 # Check for rpm SOURCES path
 printf "checking for rpm sources path... "
 RPMSOURCEDIR="NOT-FOUND"
--- x11vnc-0.8.1/acinclude.m4.trio	2006-06-24 18:44:37.453200000 +0200
+++ x11vnc-0.8.1/acinclude.m4	2006-06-24 18:45:15.143200000 +0200
@@ -603,3 +603,86 @@
 ],[PACKAGE="$PACKAGE"])])
 
 
+dnl Found this in lftp 3.0.13 and adapted it to an external trio library
+dnl Just put an include in your acinlude.m4 or aclocal.m4 file and then
+dnl call NEED_TRIO_CHECK. This will add -ltrio to LIBS if
+dnl snprint/vsnprint is unavailable.
+dnl Then make sure trio.h is included if NEED_TRIO is defined.
+dnl Something like this:
+dnl
+dnl #ifdef NEED_TRIO
+dnl #include <trio.h>
+dnl #endif
+dnl
+dnl If you're using config.h style configuration use autoheader to
+dnl automatically get the defines added. This will also add the above
+dnl snippet of code to include trio.h when needed.
+dnl
+dnl If you want trio to completely replace stdio.h then you must define
+dnl TRIO_REPLACE_STDIO. This define is currently commented and inactive.
+dnl Uncomment it as needed.
+dnl If you just want trio to take over some (missing) functions then use
+dnl AC_DEFINE and selectively use #define <func> trio_<func>
+dnl by modifying the existing AH_BOTTOM macro.
+dnl 
+dnl The original code from lftp is covered by the GNU GPL v2 and is
+dnl Copyright(c) 1996-2004 by Alexander V. Lukyanov <lav@yars.free.net>
+dnl
+dnl This adaption is also covered by the GNU GPL v2 and is
+dnl Copyright(c) 2005 by Tom G. Christensen <irixpkg@jupiterrise.com>
+dnl
+
+AC_DEFUN([NEED_TRIO_CHECK],[
+   AC_CHECK_FUNCS(vsnprintf snprintf)
+   AC_CACHE_CHECK([if trio library is needed], ac_cv_need_trio,
+   [
+      ac_cv_need_trio=no;
+
+      if test x$ac_cv_func_vsnprintf != xyes -o x$ac_cv_func_snprintf != xyes; then
+	 ac_cv_need_trio="yes (because there is no system snprintf/vsnprintf functions)"
+      else
+
+      AC_TRY_RUN([
+	 int main()
+	 {
+	    unsigned long long x=0,x1;
+	    long long y=0,y1;
+	    char buf[128];
+
+	    x=~x;
+	    sscanf("0","%llu",&x);
+	    if(x!=0) return 1;
+
+	    y=~y;
+	    sscanf("0","%lld",&y);
+	    if(y!=0) return 1;
+
+	    x=~x;
+	    sprintf(buf,"%lld %llu",y,x);
+	    sscanf (buf,"%lld %llu",&y1,&x1);
+	    if(x!=x1 || y!=y1)
+	       return 1;
+
+	    return 0;
+	 }],[],[ac_cv_need_trio="yes (because %lld fails)"],[])
+
+      fi
+   ])
+   case $ac_cv_need_trio in
+   yes*)
+      AC_CHECK_LIB(trio, trio_snprintf, [], 
+	[AC_MSG_ERROR([Unable to find trio library.])])
+      AC_DEFINE(NEED_TRIO, 1, [define if you need trio for stdio functions])
+      dnl AC_DEFINE(TRIO_REPLACE_STDIO, 1, [define if trio should replace all system stdio functions])
+      AH_BOTTOM([
+/* If we need trio then make sure that trio.h is included always */
+#if defined(NEED_TRIO) || defined(LIBVNCSERVER_NEED_TRIO)
+#include <trio.h>
+#if !defined(TRIO_REPLACE_STDIO)
+#define snprintf trio_snprintf
+#define vsnprintf trio_vsnprintf
+#endif
+#endif])
+      ;;
+   esac
+])
