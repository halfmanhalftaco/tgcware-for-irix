--- wget-1.9.1/src/sysdep.h.trio	Sat Apr  9 11:30:09 2005
+++ wget-1.9.1/src/sysdep.h	Sat Apr  9 11:30:34 2005
@@ -174,12 +174,6 @@
 #ifndef HAVE_STRPTIME
 char *strptime ();
 #endif
-#ifndef HAVE_SNPRINTF
-int snprintf ();
-#endif
-#ifndef HAVE_VSNPRINTF
-int vsnprintf ();
-#endif
 #ifndef HAVE_USLEEP
 int usleep PARAMS ((unsigned long));
 #endif
--- wget-1.9.1/src/Makefile.in.trio	Sat Apr  9 11:30:09 2005
+++ wget-1.9.1/src/Makefile.in	Sat Apr  9 11:30:34 2005
@@ -76,7 +76,7 @@
       ftp$o ftp-basic$o ftp-ls$o $(OPIE_OBJ) $(GETOPT_OBJ) hash$o \
       headers$o host$o html-parse$o html-url$o http$o init$o      \
       log$o main$o $(MD5_OBJ) netrc$o progress$o rbuf$o recur$o   \
-      res$o retr$o safe-ctype$o snprintf$o $(SSL_OBJ) url$o       \
+      res$o retr$o safe-ctype$o $(SSL_OBJ) url$o       \
       utils$o version$o
 
 .SUFFIXES:
--- wget-1.9.1/src/config.h.in.trio	Sat Apr  9 11:31:00 2005
+++ wget-1.9.1/src/config.h.in	Sat Apr  9 11:33:40 2005
@@ -332,4 +332,18 @@
 
 #endif /* NAMESPACE_TWEAKS */
 
+/* Define to 1 if you have the `trio' library (-ltrio). */
+#undef HAVE_LIBTRIO
+
+/* define if you need trio for snprint/vsnprintf functions */
+#undef NEED_TRIO
+
+#ifdef NEED_TRIO
+#include <trio.h>
+#if !defined(TRIO_REPLACE_STDIO)
+#define snprintf trio_snprintf
+#define vsnprintf trio_vsnprintf
+#endif
+#endif
+
 #endif /* CONFIG_H */
--- wget-1.9.1/aclocal.m4.trio	Sat Apr  9 11:30:09 2005
+++ wget-1.9.1/aclocal.m4	Sat Apr  9 11:30:34 2005
@@ -459,6 +459,90 @@
 AC_SUBST($1)dnl
 ])
 
+dnl Found this in lftp 3.0.13 and adapted it to an external trio library
+dnl Just put an include in your acinlude.m4 or aclocal.m4 file and then
+dnl call NEED_TRIO_CHECK. This will add -ltrio to LIBS if
+dnl snprint/vsnprint is unavailable.
+dnl Then make sure trio.h is included if NEED_TRIO is defined.
+dnl Something like this:
+dnl
+dnl #ifdef NEED_TRIO
+dnl #include <trio.h>
+dnl #endif
+dnl
+dnl If you're using config.h style configuration use autoheader to
+dnl automatically get the defines added. This will also add the above
+dnl snippet of code to include trio.h when needed.
+dnl
+dnl This particular version of needtrio.m4 will unconditionally define
+dnl TRIO_REPLACE_STDIO which means trio will take over all the *printf
+dnl functions from stdio.h. If you don't want that then remove the
+dnl AC_DEFINE an selectively use #define <func> trio_<func> instead
+dnl by modifying the existing AH_BOTTOM macro.
+dnl 
+dnl The original code from lftp is covered by the GNU GPL v2 and is
+dnl Copyright(c) 1996-2004 by Alexander V. Lukyanov <lav@yars.free.net>
+dnl
+dnl The changes in this version are by extension also covered by the
+dnl GNU GPL v2 and are
+dnl Copyright(c) 2005 by Tom G. Christensen <irixpkg@jupiterrise.com>
+dnl
+
+AC_DEFUN([NEED_TRIO_CHECK],[
+   AC_CHECK_FUNCS(vsnprintf snprintf)
+   AC_CACHE_CHECK([if trio library is needed], ac_cv_need_trio,
+   [
+      ac_cv_need_trio=no;
+
+      if test x$ac_cv_func_vsnprintf != xyes -o x$ac_cv_func_snprintf != xyes; then
+	 ac_cv_need_trio="yes (because there is no system snprintf/vsnprintf functions)"
+      else
+
+      AC_TRY_RUN([
+	 int main()
+	 {
+	    unsigned long long x=0,x1;
+	    long long y=0,y1;
+	    char buf[128];
+
+	    x=~x;
+	    sscanf("0","%llu",&x);
+	    if(x!=0) return 1;
+
+	    y=~y;
+	    sscanf("0","%lld",&y);
+	    if(y!=0) return 1;
+
+	    x=~x;
+	    sprintf(buf,"%lld %llu",y,x);
+	    sscanf (buf,"%lld %llu",&y1,&x1);
+	    if(x!=x1 || y!=y1)
+	       return 1;
+
+	    return 0;
+	 }],[],[ac_cv_need_trio="yes (because %lld fails)"],[])
+
+      fi
+   ])
+   case $ac_cv_need_trio in
+   yes*)
+      AC_CHECK_LIB(trio, trio_snprintf, [], 
+	[AC_MSG_ERROR([Error! Since snprintf/vsnprint is missing we need trio.])])
+      AC_DEFINE(NEED_TRIO, 1, [define if you need trio for snprint/vsnprintf functions])
+      dnl AC_DEFINE(TRIO_REPLACE_STDIO, 1, [define if trio should replace all system printf functions])
+      AH_BOTTOM([
+/* If we need trio then make sure that trio.h is included always */
+#ifdef NEED_TRIO
+#include <trio.h>
+#if !defined(TRIO_REPLACE_STDIO)
+#define snprintf trio_snprintf
+#define vsnprintf trio_vsnprintf
+#endif
+#endif])
+      ;;
+   esac
+])
+
 # Include libtool code.
 
 builtin(include, libtool.m4)dnl
--- wget-1.9.1/configure.in.trio	Sat Apr  9 11:30:09 2005
+++ wget-1.9.1/configure.in	Sat Apr  9 11:30:34 2005
@@ -193,7 +193,8 @@
 AC_FUNC_ALLOCA
 AC_FUNC_MMAP
 AC_CHECK_FUNCS(strdup strstr strcasecmp strncasecmp strpbrk memmove)
-AC_CHECK_FUNCS(gettimeofday mktime strptime strerror snprintf vsnprintf)
+AC_CHECK_FUNCS(gettimeofday mktime strptime strerror)
+NEED_TRIO_CHECK
 AC_CHECK_FUNCS(select sigblock sigsetjmp signal symlink access isatty)
 AC_CHECK_FUNCS(uname gethostname usleep)
 
